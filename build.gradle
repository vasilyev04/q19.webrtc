buildscript {
    ext.kotlin_version = '1.6.21'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.8.0'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

/**
 * Shared configs across subprojects
 */
ext.projectArtifactId = { project ->
    switch (project.name) {
        // Single module
        case 'webrtc':
            return project.name
    }
    return null
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    group = 'com.github.vasilyev04'
    project.ext.version_code = 10
    version = '1.6.0'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    if (project.ext.artifactId != null) {
        println "ArtifactId [${project.ext.artifactId}] is set"
    }
}

static def getSampleModuleName(project) {
    if (project.name == "sample") {
        println "Project [${project.name}] is sample module"
        return project.name
    }
    return null
}

/**
 * Publishing and signing info
 */
subprojects { project ->
    println "Subproject: ${project.name}"

    if (getSampleModuleName(project) != null) return

    apply plugin: 'com.android.library'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin-android'

    android {
        compileSdkVersion 34

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 34

            versionCode project.ext.version_code
            versionName project.version
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        buildFeatures {
            buildConfig false
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_17.toString()
        }
    }

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    url = "https://github.com/zhombie/q19.webrtc"

                    developers {
                        developer {
                            name = 'zhombie'
                        }

                        developer {
                            name = "vasilyev04"
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

}


task clean(type: Delete) {
    delete rootProject.buildDir
}